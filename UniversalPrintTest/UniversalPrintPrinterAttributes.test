#
# "$Id: UnviersalPrintPrinterAttributes.test 11396 2013-11-06 20:09:03Z msweet $"
#
#   IPP/UnviersalPrintPrinterAttributes test suite.
#
#   Copyright 2007-2013 by Apple Inc.
#   Copyright 2001-2006 by Easy Software Products. All rights reserved.
#
#   These coded instructions, statements, and computer programs are the
#   property of Apple Inc. and are protected by Federal copyright
#   law.  Distribution and use rights are outlined in the file "LICENSE.txt"
#   which should have been included with this file.  If this file is
#   file is missing or damaged, see the license at "http://www.cups.org/".
#
# Usage:
#
#   ./ipptool -t printer-uri UnviersalPrintPrinterAttributes.test
#

# Regular expressions for URI schemes:
#
#   HTTP_URI_SCHEME - Matches strings beginning with http:// or https://
#   IPP_URI_SCHEME  - Matches strings beginning with ipp:// or ipps://

DEFINE HTTP_URI_SCHEME "/^https?://.+$$/"
DEFINE IPP_URI_SCHEME "/^ipps?://.+$$/"

# Test Printer description attributes
#
# Required by: RFC 8011 section 5.2.1
{

        NAME "PWG 5100.14 section 5.1/5.2 - Required Operations and Attributes"
	OPERATION Get-Printer-Attributes
	GROUP operation-attributes-tag
	ATTR charset attributes-charset utf-8
	ATTR naturalLanguage attributes-natural-language en
	ATTR uri printer-uri $uri
	ATTR keyword requested-attributes all,media-col-database

	STATUS successful-ok
        
        EXPECT copies-default OF-TYPE integer IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE >0
        EXPECT copies-supported OF-TYPE rangeOfInteger IN-GROUP printer-attributes-tag

	EXPECT document-format-default OF-TYPE mimeMediaType IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM document-format-supported
        EXPECT document-format-supported OF-TYPE mimeMediaType IN-GROUP printer-attributes-tag
	EXPECT document-format-supported OF-TYPE mimeMediaType IN-GROUP printer-attributes-tag WITH-VALUE "application/PCLm" DEFINE-MATCH APPLICATION_PCLM_SUPPORTED
	EXPECT document-format-supported OF-TYPE mimeMediaType IN-GROUP printer-attributes-tag WITH-VALUE "application/pdf" DEFINE-MATCH APPLICATION_PDF_SUPPORTED
	EXPECT document-format-supported OF-TYPE mimeMediaType IN-GROUP printer-attributes-tag WITH-VALUE "image/pwg-raster" DEFINE-MATCH IMAGE_PWG_RASTER_SUPPORTED

	EXPECT finishings-default OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE-FROM finishings-supported
	EXPECT finishings-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3

	EXPECT media-col-database OF-TYPE collection IN-GROUP printer-attributes-tag
	EXPECT media-col-default OF-TYPE collection IN-GROUP printer-attributes-tag COUNT 1
	EXPECT media-col-supported OF-TYPE keyword IN-GROUP printer-attributes-tag
	
	EXPECT media-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag
	
	EXPECT media-col-database/media-size OF-TYPE collection
	EXPECT media-col-database/media-source-properties/media-source-feed-direction OF-TYPE keyword
        EXPECT media-col-database/media-type OF-TYPE keyword|name DEFINE-MATCH MEDIA_COL_DATABASE_HAS_MEDIA_TYPE
	EXPECT media-type-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag IF-NOT-DEFINED MEDIA_COL_DATABASE_HAS_MEDIA_TYPE

        EXPECT media-type-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag DEFINE-MATCH MEDIA_TYPE_SUPPORTED_PRESENT
	EXPECT media-col-default/media-type DEFINE-MATCH MEDIA_TYPE_PRESENT_IN_MEDIA_COL_DEFAULT
	EXPECT media-col-default/media-type WITH-VALUE-FROM media-type-supported IF-DEFINED MEDIA_TYPE_SUPPORTED_PRESENT IF-DEFINED MEDIA_TYPE_PRESENT_IN_MEDIA_COL_DEFAULT
	
	EXPECT ?number-up-default OF-TYPE integer IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE >0
	EXPECT ?number-up-supported OF-TYPE integer|rangeOfInteger IN-GROUP printer-attributes-tag WITH-VALUE >0

	EXPECT orientation-requested-default OF-TYPE enum IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM orientation-requested-supported
	EXPECT orientation-requested-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3,4,5,6

	EXPECT output-bin-default OF-TYPE keyword|name IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM output-bin-supported
	EXPECT output-bin-supported OF-TYPE keyword|name IN-GROUP printer-attributes-tag

	EXPECT pages-ranges-supported OF-TYPE boolean IN-GROUP printer-attributes-tag IF-DEFINED APPLICATION_PCLM_SUPPORTED
	
	EXPECT pclm-raster-back-side OF-TYPE keyword IN-GROUP printer-attributes-tag IF-DEFINED APPLICATION_PCLM_SUPPORTED
	EXPECT pclm-source-resolution-supported OF-TYPE resolution IN-GROUP printer-attributes-tag IF-DEFINED APPLICATION_PCLM_SUPPORTED
	EXPECT pclm-strip-height-preferred OF-TYPE integer IN-GROUP printer-attributes-tag WITH-VALUE-FROM pclm-strip-height-supported IF-DEFINED APPLICATION_PCLM_SUPPORTED
	EXPECT pclm-strip-height-supported OF-TYPE integer IN-GROUP printer-attributes-tag IF-DEFINED APPLICATION_PCLM_SUPPORTED

	EXPECT print-color-mode-default OF-TYPE keyword IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM print-color-mode-supported
	EXPECT print-color-mode-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-ALL-VALUES "/^(auto|auto-monochrome|bi-level|monochrome|color|highlight|process-bi-level|process-monochrome)$/"
	EXPECT print-color-mode-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE "monochrome"

	EXPECT printer-is-accepting-jobs OF-TYPE boolean IN-GROUP printer-attributes-tag COUNT 1
	EXPECT pdf-k-octets-supported OF-TYPE rangeOfInteger IN-GROUP printer-attributes-tag IF-DEFINED APPLICATION_PDF_SUPPORTED
	EXPECT pdf-versions-supported OF-TYPE keyword IN-GROUP printer-attributes-tag IF-DEFINED APPLICATION_PDF_SUPPORTED

	EXPECT print-quality-default OF-TYPE enum IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM print-quality-supported
	EXPECT print-quality-supported OF-TYPE enum IN-GROUP printer-attributes-tag WITH-VALUE 3,4,5

	EXPECT printer-make-and-model OF-TYPE text IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE "/^.{0,127}$$/"

	EXPECT printer-more-info OF-TYPE uri IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE "$HTTP_URI_SCHEME"

	EXPECT printer-resolution-default OF-TYPE resolution IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM printer-resolution-supported
	EXPECT printer-resolution-supported OF-TYPE resolution IN-GROUP printer-attributes-tag
	EXPECT print-scaling-default OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM print-scaling-supported IF-DEFINED APPLICATION_PDF_SUPPORTED
	EXPECT print-scaling-supported OF-TYPE keyword IN-GROUP printer-attributes-tag IF-DEFINED APPLICATION_PDF_SUPPORTED
	EXPECT print-scaling-default OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE-FROM print-scaling-supported IF-DEFINED IMAGE_PWG_RASTER_SUPPORTED
	EXPECT print-scaling-supported OF-TYPE keyword IN-GROUP printer-attributes-tag IF-DEFINED IMAGE_PWG_RASTER_SUPPORTED
	
	EXPECT printer-state OF-TYPE enum IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE 3,4,5
	EXPECT printer-state-reasons OF-TYPE keyword IN-GROUP printer-attributes-tag
	EXPECT printer-location OF-TYPE text IN-GROUP printer-attributes-tag WITH-VALUE "/^.{0,127}$$/"

	EXPECT sides-default OF-TYPE keyword IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM sides-supported
	EXPECT sides-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE "/^(one-sided|two-sided-long-edge|two-sided-short-edge)$$/"

	EXPECT charset-configured OF-TYPE charset IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM charset-supported
	EXPECT charset-supported OF-TYPE charset IN-GROUP printer-attributes-tag WITH-VALUE utf-8

	EXPECT natural-language-configured OF-TYPE naturalLanguage IN-GROUP printer-attributes-tag COUNT 1 WITH-VALUE-FROM generated-natural-language-supported
	EXPECT generated-natural-language-supported OF-TYPE naturalLanguage IN-GROUP printer-attributes-tag

	EXPECT ipp-versions-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE "/^(1.0|1.1|2.0)$$/"
	EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag
	EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag COUNT 1 DEFINE-MATCH IPP_FEATURES_HAS_SINGLE_VALUE
	EXPECT ipp-features-supported OF-TYPE keyword IN-GROUP printer-attributes-tag WITH-VALUE none IF-DEFINED IPP_FEATURES_HAS_SINGLE_VALUE

	EXPECT operations-supported OF-TYPE enum IN-GROUP printer-attributes-tag

	EXPECT pwg-raster-document-resolution-supported OF-TYPE resolution IN-GROUP printer-attributes-tag IF-DEFINED IMAGE_PWG_RASTER_SUPPORTED
	EXPECT pwg-raster-document-sheet-back OF-TYPE keyword IN-GROUP printer-attributes-tag IF-DEFINED IMAGE_PWG_RASTER_SUPPORTED
	EXPECT pwg-raster-document-type-supported OF-TYPE keyword IN-GROUP printer-attributes-tag IF-DEFINED IMAGE_PWG_RASTER_SUPPORTED
}
